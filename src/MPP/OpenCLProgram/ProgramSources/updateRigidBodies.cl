  /*
  
    Kernel to calculate the "macroscopic" attributes of the rigid bodies with help of the results
    from the previous particle integrations;
  
    Formulas are taken from the Diploma thesis of Thomas Steil, "Efficient Methods for Computational Fluid Dynamics and Interactions",
    ยง3.2.4 "Rigid bodies in particle system";
    
    IMPORTANT: pass numArraysToScanInParallel = 9 to grantlee, else not all attributes and dimensions will be summed up
    
    
    The update procedure for the particles works as follows:
      1. new positions and new (corrected) particle velocities are read to private memory;
      2. those values are "divided into dimensions" and copied into local memory for future "multi-half-scan";
        layout (padding not illustrated, every line corresponds to one array which is scanned;): 
          posX_part0,     posX_part1 ...    posX_partn-1, 
          posY_part0,     posY_part1 ...    posY_partn-1, 
          posZ_part0,     posZ_part1 ...    posZ_partn-1, 
          linVelX_part0,  linVelX_part1 ... linVelX_partn-1, 
          .
          .
          .
          angVelZ_part0,  angVelZ_part1 ... angVelZ_partn-1;
        The advantage of this layout is that we have only as much control flow overhaed as for the scan of a single array,
        while scanning multiple arrays :D.
        The disadvantage is that this costs x times as much local memory :(.
      3. old particle positions are read to private memory, old RB centre of mass is subtracted to yield rotated old relative positions
      4. "particle angular velocity" is calculated via cross( particleRotatedRelativeOldPos, particleNewVel );  
      5. "particle angular velocity" is divided and copied like in step 2.;
      6  A "multi-half-scan" is performed on the divided attributes to yield the total sums for each attribute dimension;
      7. New centre of mass, new linear velocity and new angular velocity of RB is computed from those values and stored to local mem;
      8. A rotation matrix is computed from the angular velocity (considering length, direction and time step);
      9.a.  The new transformation matrix to be applied on non-rotated relative particle positions is computed as follows:
              newXformMat = translate(newCentreOfMassPos) * newAngVelRotMat * rotationalPart(oldXformMat) ;
            This matrix is written to the local mem instance of the rigid body worked on by this work group;
      9.b.  Alternatively, if we wanna influence the transform by the app (because this is more precise than messing around with forces),
            e.g. to make a rigid body strive to a certain transform relative to the user (to act as a paddle, tool, weapon...),
            we can deliver a "corrective transformation matrix" containing an app-computed time- and-target orientation dependent
            rotational and relative translational part;
            Then, the transformation matrix looks like this:
            newXformMat =   translate(newCentreOfMassPos) * translate(translationalPart(correctiveXformMat))
                            rotationalPart(correctiveXformMat) newAngVelRotMat * rotationalPart(oldXformMat) ;
     10. The new corrected particle velocity is computed as follows:
          newCorrParticleVel = newRBLinVel + cross( newRBAngVel, oldRotatedRelativeParticlePos );
     11. the new particle position is computed:
          newParticlePos = newXformMat * relativeParticlePos;
          We always use the initial unrotated relative positions to omit accumulation numerical errors when using the already- transformed
          positions from the previous passes!
     12. new predicted particle velicity is:
          (newParticlePos - oldParticlePos) / timestep;
               
      
      
      
      TODO doing half scans in sequence, working with intermediate result, is relatively straight forward;
      When this is implemented, ther will be no technical restriction to particle count of a rigid body but the total particle
      count of the whole particle scene ;( ;
    
  */

  
  #pragma OPENCL EXTENSION cl_nv_pragma_unroll : enable
  #pragma  OPENCL EXTENSION cl_khr_global_int32_base_atomics : enable
  //pragma OPENCL EXTENSION cl_khr_global_int32_extended_atomics : enable
  #pragma OPENCL EXTENSION cl_khr_local_int32_base_atomics : enable
  //pragma OPENCL EXTENSION cl_khr_local_int32_extended_atomics : enable    
      
    
  {% include "scan.cl" %}
  {% include "matrixMath.cl" %}
   
  
  #define NUM_WORK_ITEMS_PER_WORK_GROUP ( NUM_MAX_PARTICLES_PER_RIGID_BODY / 2 )
     
   

  enum MacroscopicRBFeatures
  {
    CENTRE_OF_MASS_POSITION_X,
    CENTRE_OF_MASS_POSITION_Y,
    CENTRE_OF_MASS_POSITION_Z,
    LINEAR_VELOCITY_X,
    LINEAR_VELOCITY_Y,
    LINEAR_VELOCITY_Z,
    ANGULAR_VELOCITY_X,
    ANGULAR_VELOCITY_Y,
    ANGULAR_VELOCITY_Z,
    NUM_MACROSCOPIC_RB_PHYSICAL_QUANTITIES
  };
      
      
  __kernel
  __attribute__((reqd_work_group_size( NUM_WORK_ITEMS_PER_WORK_GROUP ,1,1)))  
  void
  updateRigidBodies
  (
    __constant SimulationParameters* cSimParams,
    __constant ObjectGenericFeatures* cObjectGenericFeatures,

    __global uint* gParticleIndexTable, //for finding the particles belonging to the current rigid body
        
   
    
    __global float4* gParticlePositionsOld, //the old positions are not refreshed by particle integration of current pass,
                                            //hence (gParticlePositionsOld[index] - rigidBody.oldCentreOfMass) encodes the rotated 
                                            //relative position of a particle from the previous pass
    __global float4* gParticlePositionsNew, //read for new centre of mass computation and written 
                                            //for new-RB-transform-alligned particle world positions;

    //read for caculation of new linear and angular velocity;
    __global float4* gParticleCorrectedVelocities,
    
    //write only
    __global float4* gParticlePredictedVelocities,
        
    //numRigidBodies elements    
    __global ParticleRigidBody* gRigidBodies,
    //numRigidBodies * NUM_MAX_PARTICLES_PER_RIGID_BODY elements
    __global float4* gRigidBodyRelativePositions
       
  )
  {
    //used 9 times for a "half scan"
    __local float lScanArray[ PADDED_STRIDE( NUM_MACROSCOPIC_RB_PHYSICAL_QUANTITIES * NUM_MAX_PARTICLES_PER_RIGID_BODY ) ];
    //9 elements holding the total sums of world positions, linear and angular velocities after "multi-array-scan"
    __local float lMacroscopicPhysicalAttributes[ NUM_MACROSCOPIC_RB_PHYSICAL_QUANTITIES ];
    

    __local float16 tempMatrices[2];
    
    __local ParticleRigidBody lRigidBody;
    
                                           

    
    uint lwiID = get_local_id(0); // short for "local work item ID"
    uint gwiID = get_global_id(0); // short for "global work item ID"
    uint groupID =  get_group_id(0); //group ID equvalent to rigid body ID in this kernel
    
    
    //{ grab rigid body to local memory in a hacky and hence hopefully efficient way ;) :
      if( lwiID < sizeof(ParticleRigidBody)/4 )
      {
        ( (__local uint* ) ( & ( lRigidBody ) ) )
              [ lwiID ]  
          =  
        ( (__global uint*) ( & ( gRigidBodies[ groupID ] ) ) )
              [ lwiID ] ;
      } 
    //}    
    
    //-------------------------------------------------------------------------------------------------------    
    uint localPaddedParticleIndexLower = CONFLICT_FREE_INDEX( lwiID );
    uint localPaddedParticleIndexHigher = CONFLICT_FREE_INDEX( lwiID + (NUM_MAX_PARTICLES_PER_RIGID_BODY/2) );
    
    uint globalParticleIndexLower;
    uint globalParticleIndexHigher;
    //-------------------------------------------------------------------------------------------------------
    
    //-------------------------------------------------------------------------------------------------------
    float4 particlePosLowerIndex;
    float4 particlePosHigherIndex;
    
    float4 particleVelLowerIndex;
    float4 particleVelHigherIndex;
    //-------------------------------------------------------------------------------------------------------
    
    
    if( lwiID < ( lRigidBody.numContainingParticles >>1 ) )
    {   
      //globalParticleIndexLower  = gParticleIndexTable[ groupID * NUM_MAX_PARTICLES_PER_RIGID_BODY
      globalParticleIndexLower  = gParticleIndexTable[
                                  //group ID equivalent to rigid body ID in this kernel 
          cObjectGenericFeatures[ groupID +  RIGID_BODY_OBJECT_START_INDEX ].offsetInIndexTableBuffer
          + lwiID 
        ];
      //globalParticleIndexHigher = gParticleIndexTable[ groupID * NUM_MAX_PARTICLES_PER_RIGID_BODY                                                                     
      globalParticleIndexHigher = gParticleIndexTable[
                                  //group ID equivalent to rigid body ID in this kernel 
          cObjectGenericFeatures[ groupID +  RIGID_BODY_OBJECT_START_INDEX ].offsetInIndexTableBuffer
          + lwiID + (NUM_MAX_PARTICLES_PER_RIGID_BODY/2) 
        ];
    
      // 1. new positions and new (corrected) particle velocities are read to private memory;
      //    The new positions are resulting from particle integrationand are used here calculate the new centre of mass   
      particlePosLowerIndex = gParticlePositionsNew[ globalParticleIndexLower ]; 
      particlePosHigherIndex = gParticlePositionsNew[ globalParticleIndexHigher ]; 
      //    The new corrected velocities are resulting from particle integration and are used here calculate the new linear velocity;
      particleVelLowerIndex = gParticleCorrectedVelocities[ globalParticleIndexLower ]; 
      particleVelHigherIndex = gParticleCorrectedVelocities[ globalParticleIndexHigher ]; 
    }
    else
    {
      //particle with this index doesn't exist for this rigid body, but we need to operate on base2 arrays,
      //hence init the rest to zero; 
      particlePosLowerIndex   = (float4) (0.0f,0.0f,0.0f,1.0f);
      particlePosHigherIndex  = (float4) (0.0f,0.0f,0.0f,1.0f);
      particleVelLowerIndex  = (float4) (0.0f,0.0f,0.0f,0.0f);
      particleVelHigherIndex = (float4) (0.0f,0.0f,0.0f,0.0f);
    }
    
    //2. those values are "divided into dimensions" and copied into local memory for future "multi-half-scan";
    
    //reorder new world positions into "multiscan array" for scanning each dimension seperately 
    //to yield the new centre of mass position; 
    lScanArray[ PADDED_STRIDE( CENTRE_OF_MASS_POSITION_X * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particlePosLowerIndex.x;
    lScanArray[ PADDED_STRIDE( CENTRE_OF_MASS_POSITION_X * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particlePosHigherIndex.x;
    lScanArray[ PADDED_STRIDE( CENTRE_OF_MASS_POSITION_Y * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particlePosLowerIndex.y;
    lScanArray[ PADDED_STRIDE( CENTRE_OF_MASS_POSITION_Y * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particlePosHigherIndex.y;
    lScanArray[ PADDED_STRIDE( CENTRE_OF_MASS_POSITION_Z * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particlePosLowerIndex.z;
    lScanArray[ PADDED_STRIDE( CENTRE_OF_MASS_POSITION_Z * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particlePosHigherIndex.z;
      
    //reorder new corrected linear velocities into "multiscan array" for scanning each dimension seperately
    //to yield the new linear velocity; 
    lScanArray[ PADDED_STRIDE( LINEAR_VELOCITY_X * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particleVelLowerIndex.x;
    lScanArray[ PADDED_STRIDE( LINEAR_VELOCITY_X * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particleVelHigherIndex.x;
    lScanArray[ PADDED_STRIDE( LINEAR_VELOCITY_Y * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particleVelLowerIndex.y;
    lScanArray[ PADDED_STRIDE( LINEAR_VELOCITY_Y * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particleVelHigherIndex.y;
    lScanArray[ PADDED_STRIDE( LINEAR_VELOCITY_Z * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particleVelLowerIndex.z;
    lScanArray[ PADDED_STRIDE( LINEAR_VELOCITY_Z * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particleVelHigherIndex.z;  
       
    float4 particleRelativeRotatedPosLowerIndex;
    float4 particleRelativeRotatedPosHigherIndex;

    //we now need the relative and rotated particle positions from the previous pass and the angular velocities:
    if( lwiID < ( lRigidBody.numContainingParticles >>1 ) )
    {      
      //relative positions from last simulation pass (better do some on-the-fly calculations than wasting memory and bandwidth and
      //destroying particle element buffer symmetry^^)
      particlePosLowerIndex = gParticlePositionsOld[ globalParticleIndexLower ];
      particlePosHigherIndex = gParticlePositionsOld[ globalParticleIndexHigher ];
      
      particleRelativeRotatedPosLowerIndex = particlePosLowerIndex  - lRigidBody.centreOfMassPosition ; 
      particleRelativeRotatedPosHigherIndex = particlePosHigherIndex - lRigidBody.centreOfMassPosition ; 
      
      
      //angular velocity of particle: relative position x linear velocity;
      particleVelLowerIndex = cross( particleRelativeRotatedPosLowerIndex, particleVelLowerIndex );
      particleVelHigherIndex = cross( particleRelativeRotatedPosHigherIndex, particleVelHigherIndex );
    } //no else here, stays zero ;)
    
    //reorder new corrected  angular velocities into "multiscan array" for scanning each dimension seperately
    //to yield the new angular velocity; 
    lScanArray[ PADDED_STRIDE( ANGULAR_VELOCITY_X * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particleVelLowerIndex.x;
    lScanArray[ PADDED_STRIDE( ANGULAR_VELOCITY_X * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particleVelHigherIndex.x;
    lScanArray[ PADDED_STRIDE( ANGULAR_VELOCITY_Y * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particleVelLowerIndex.y;
    lScanArray[ PADDED_STRIDE( ANGULAR_VELOCITY_Y * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particleVelHigherIndex.y;
    lScanArray[ PADDED_STRIDE( ANGULAR_VELOCITY_Z * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexLower ] = 
      particleVelLowerIndex.z;
    lScanArray[ PADDED_STRIDE( ANGULAR_VELOCITY_Z * NUM_MAX_PARTICLES_PER_RIGID_BODY ) + localPaddedParticleIndexHigher ] = 
      particleVelHigherIndex.z;  

    
    scanExclusive_upSweep(lScanArray, lMacroscopicPhysicalAttributes,  NUM_MAX_PARTICLES_PER_RIGID_BODY, lwiID);
    
    
    
   //----------------------------------------------------------------
   //We now have the macroscopic pos. and vel. values;
   //We have to construct a rotation matrix now to restore the old relative positions according to the new macroscpic features;
   //otherwise, the RB-particle cloud would dissipate just like the fluid particles;
   if( lwiID == 0 )
   {
     //no need for synch yet, as work item 0 has done all the "total sum" stuff;
     //note; instead of using 3 thread to parllize the below downscale, I rather save one if() statement;
     //and do alltransformations matrix building with one single work item;
     

     //overwrite the old values, we don't need them anymore, an save register usage this way;
     
     //padded index of last element of each "feature dimension array"
     #define INDEX_OF_TOTAL_SUM( featureEnum ) ( PADDED_STRIDE( ( ( featureEnum +1 ) * NUM_MAX_PARTICLES_PER_RIGID_BODY ) -1 ) )

     lRigidBody.linearVelocity = (float4) (
       lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( LINEAR_VELOCITY_X ) ]  * lRigidBody.inverseNumContainingParticles, 
       lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( LINEAR_VELOCITY_Y ) ]  * lRigidBody.inverseNumContainingParticles, 
       lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( LINEAR_VELOCITY_Z ) ]  * lRigidBody.inverseNumContainingParticles,
       0.0f     
     );
    lRigidBody.angularVelocity = (float4) (
       lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( ANGULAR_VELOCITY_X ) ]  * lRigidBody.inverseTotalSquaredDistancesFromCenterOfMass, 
       lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( ANGULAR_VELOCITY_Y ) ]  * lRigidBody.inverseTotalSquaredDistancesFromCenterOfMass, 
       lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( ANGULAR_VELOCITY_Z ) ]  * lRigidBody.inverseTotalSquaredDistancesFromCenterOfMass,
       0.0f     
     ); 
      
     //{ 8. A rotation matrix is computed from the angular velocity (considering length, direction and time step); 
     float lengthAngVel= length(lRigidBody.angularVelocity);
     float4 rotationAxis = lRigidBody.angularVelocity / lengthAngVel; //TODO  native_divide
     float rotationAngleRadians = lengthAngVel * cSimParams->timestep;

     // 9.b.  If we wanna influence the transform by the app (because this is more precise than messing around with forces),
     //       e.g. to make a rigid body strive to a certain transform relative to the user (to act as a paddle, tool, weapon...),
     //       we can deliver a "corrective transformation matrix" containing an app-computed time- and-target orientation dependent
     //       rotational and relative translational part;
     //       Then, the transformation matrix looks like this:
     //       newXformMat =   translate(newCentreOfMassPos) * translate(translationalPart(correctiveXformMat))
     //                       rotationalPart(correctiveXformMat) newAngVelRotMat * rotationalPart(oldXformMat) ;
     
     
     constructRotationMatrix( &( tempMatrices[0] ), rotationAxis, rotationAngleRadians );
     //} end step 8
     //tmp0 = newAngVelRotMat = constructRotationMatrix(...);
     //tmp1 = tmp0            * rotationalPart(oldXformMat);
     //     = newAngVelRotMat * rotationalPart(oldXformMat);
     matrixMult3x3( 
      &( tempMatrices[1] ), 
      &( tempMatrices[0] ) , 
      &( ROTATIONAL_PART( lRigidBody.transformationMatrix ) ) 
     );
     
     //newXformMat = rotationalPart(correctiveXformMat) * tmp1;
     //            = rotationalPart(correctiveXformMat) * newAngVelRotMat * rotationalPart(oldXformMat);
     //(rot. part implicit due to 3x3 multiplication)
     matrixMult3x3( 
      & lRigidBody.transformationMatrix,  
      &( ROTATIONAL_PART( lRigidBody.correctiveTransformationMatrix ) ), 
      &( tempMatrices[1] ) 
     );
     
     
     //set the translation: position sums/numParticles + corrective translation
     TRANSLATION_VEC_OF_MATRIX( lRigidBody.transformationMatrix ) =
       = (float4) (
         lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( CENTRE_OF_MASS_POSITION_X ) ] * lRigidBody.inverseNumContainingParticles, 
         lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( CENTRE_OF_MASS_POSITION_Y ) ] * lRigidBody.inverseNumContainingParticles, 
         lMacroscopicPhysicalAttributes[ INDEX_OF_TOTAL_SUM( CENTRE_OF_MASS_POSITION_Z ) ] * lRigidBody.inverseNumContainingParticles,
         0.0f //make zero because the addition of the transl. vec of the correctiveTransformationMatrix will restore the "1.0f"
       )
       + TRANSLATION_VEC_OF_MATRIX( lRigidBody.correctiveTransformationMatrix );
     

  }
      
  //synch so that every thread can read the updated RB values;
  barrier(CLK_LOCAL_MEM_FENCE);
  
     
  if( lwiID < ( lRigidBody.numContainingParticles >>1 ) )
  {    
    //rotate to the new rotated relative position (needed for particle speed calcs because cross product with angluar velocity)
    particleRelativeRotatedPosLowerIndex  =
      matrixMult3x3Vec(
          &( ROTATIONAL_PART( lRigidBody.transformationMatrix ) ), 
          gRigidBodyRelativePositions[ 
            groupID * NUM_MAX_PARTICLES_PER_RIGID_BODY 
            + lwiID 
          ] 
      );
    
    particleRelativeRotatedPosHigherIndex =
      matrixMult3x3Vec(
          &( ROTATIONAL_PART( lRigidBody.transformationMatrix ) ), 
          gRigidBodyRelativePositions[ 
            groupID * NUM_MAX_PARTICLES_PER_RIGID_BODY
            + lwiID + (NUM_MAX_PARTICLES_PER_RIGID_BODY/2)  
          ] 
      );
  
    gParticleCorrectedVelocities[ globalParticleIndexLower ] = 
      lRigidBody.linearVelocity + cross(lRigidBody.angularVelocity, particleRelativeRotatedPosLowerIndex);
    gParticleCorrectedVelocities[ globalParticleIndexHigher ] = 
      lRigidBody.linearVelocity + cross(lRigidBody.angularVelocity, particleRelativeRotatedPosHigherIndex);

    //pray for automatic register renaming, otherwise, we have the danger of spiiling to global mem!
    //(edit: because of the "multi scan array", the bottleneck is now rather the local memory and not the register file anymore ;( 
    //;anyway, saving any kind of resources is always a good thing, as long the code readability donesn't get TOO much screwed up ;( )
    
    //11. the new particle position is computed:
    //      newParticlePos = newXformMat * relativeParticlePos;
    //      We always use the initial unrotated relative positions to omit accumulation numerical errors when using the already- transformed
    //      positions from the previous passes!
    //      Note that instead of a 4x4 matrx mult , we do ra 3x3 rotation and than add the translation;
    //      This way, we save four mults and tree adds for the obsolet fourth matrix row;
    float4 particleNewPosLowerIndex =
      particleRelativeRotatedPosLowerIndex + TRANSLATION_VEC_OF_MATRIX( lRigidBody.transformationMatrix );
    float4 particleNewPosHigherIndex =  
      particleRelativeRotatedPosHigherIndex + TRANSLATION_VEC_OF_MATRIX( lRigidBody.transformationMatrix );
      
    gParticlePositionsNew[ globalParticleIndexLower ] = particleNewPosLowerIndex;
    gParticlePositionsNew[ globalParticleIndexHigher ] = particleNewPosHigherIndex;
   
    //update also predicted values; don't corrupt the integration scheme ;P
    gParticlePredictedVelocities[ globalParticleIndexLower ] = 
        ( particleNewPosLowerIndex - particlePosLowerIndex ) * cSimParams->inverseTimestep;
    gParticlePredictedVelocities[ globalParticleIndexHigher ] = 
        ( particleNewPosHigherIndex - particlePosHigherIndex ) * cSimParams->inverseTimestep;
    
   }
    
    
   //{ upload rigid body to global memory in a hacky and hence hopefully efficient because parallel way ;) :
      if( lwiID < sizeof(ParticleRigidBody)/4 )
      {
        ( (__global uint*) ( & ( gRigidBodies[ groupID ] ) ) )
              [ lwiID ]  
          =  
        ( (__local uint* ) ( & ( lRigidBody ) ) )
              [ lwiID ] ;
      } 
   //}  
    
    
    
    
      
  }

